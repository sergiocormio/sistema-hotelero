<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%" creationComplete="init()">
	<fx:Metadata> 
		[Event(name="cancelClicked", type="flash.events.Event")]
		[Event(name="saveSuccess", type="flash.events.Event")]
	</fx:Metadata> 
	<fx:Script>
		<![CDATA[
			import mx.validators.Validator;
			import model.Rate;
			import model.RatePK;
			import model.Season;
			
			import mx.collections.ArrayCollection;
			import mx.containers.TitleWindow;
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import resources.Colors;
			import resources.icons.Icons;
			
			import services.crud.RateService;
			import services.crud.RoomTypeService;
			import services.crud.SeasonService;
			
			import utils.WindowsUtils;
			
			[Bindable]
			public var loc:Object;
			
			[Bindable]
			public var isUpdate:Boolean;
			
			//SEASON
			[Bindable]
			private var seasons:ArrayCollection = new ArrayCollection();
			
			public var seasonPreSelected:Season;
			
			private var seasonForm:SeasonIndividualCRUDForm;
			//ROOM TYPE			
			[Bindable]
			private var roomTypes:ArrayCollection = new ArrayCollection();
			
			public var roomTypePreSelected:Season;
			
			private var roomTypeForm:RoomTypeIndividualCRUDForm;
			
			private var titleWindow:TitleWindow;
			private var _currentRate:Rate;
			
			public function get currentRate():Rate
			{
				return _currentRate;
			}
			
			public function set currentRate(value:Rate):void
			{
				_currentRate = value;
				
				if(_currentRate.id != null){
					WindowsUtils.autoSelectComboBox(seasonsCombo,_currentRate.id.season);
					WindowsUtils.autoSelectComboBox(roomTypesCombo,_currentRate.id.roomType);
				}

				//avoids showing NaN
				if(_currentRate.price >= 0){
					priceText.text = _currentRate.price.toString();
				}else{
					priceText.text = '';
				}
			
			}
			
			private function allFieldsAreValid():Boolean{
				
				setEnabledValidators(true);
				
				var valArray:Array = new Array();
				
				valArray.push(seasonValidator);
				valArray.push(roomTypeValidator);
				valArray.push(seasonPriceValidator);
				
				var validatorErrorArray:Array = Validator.validateAll(valArray);;
				var isValidForm:Boolean = validatorErrorArray.length == 0;
				
				setEnabledValidators(false);
				
				return isValidForm;
				
			}
			
			private function setEnabledValidators(enabled:Boolean):void{
				seasonValidator.enabled = enabled;
				roomTypeValidator.enabled = enabled;
				seasonPriceValidator.enabled = enabled;
			}
			
			private function save():void{
				// validate all data
				if(!allFieldsAreValid()){
					return;
				}
				
				//_currentRate.name = rateText.text;
				
				//fills all fields
				if(_currentRate.id == null){
					_currentRate.id = new RatePK();
				}
				_currentRate.id.season = seasonsCombo.selectedItem;
				_currentRate.id.roomType = roomTypesCombo.selectedItem;
				_currentRate.price = Number(priceText.text);
				
				//calls service
				if(isUpdate){
					new RateService().updateRecord(_currentRate,rateSaved_resultHandler,faultHandler);
				}else{
					new RateService().createRecord(_currentRate,rateSaved_resultHandler,faultHandler);
				}
			}
			
			private function rateSaved_resultHandler(event:ResultEvent,token:Object=null):void {
				_currentRate = event.result as Rate;
				dispatchEvent(new Event("saveSuccess"));
			}
			
			private function cancel():void{
				dispatchEvent(new Event("cancelClicked"));
			}
			
			// Handle a message fault.
			private function faultHandler(event:FaultEvent,token:Object=null):void {
				var errCode:String = event.fault.rootCause.errorCode;
				
				Alert.show( loc.validator.server.child(errCode).text(), loc.validator.error );
			}
			
			protected function init():void
			{
				currentRate = new Rate();
				//rateText.setFocus();
				retrieveRoomTypes();
				retrieveSeasons();
			}
			
			private function retrieveSeasons():void{
				var seasonServ:SeasonService = new SeasonService();
				seasonServ.retrieveAll(retrieveSeasons_resultHandler,faultHandler);
			}
			
			private function retrieveSeasons_resultHandler(event:ResultEvent,token:Object=null):void {
				//fills the ComboBox
				seasons.removeAll();
				seasons.addAll(event.result as ArrayCollection);
				//preselects a season
				if(seasonPreSelected != null){
					WindowsUtils.autoSelectComboBox(seasonsCombo,seasonPreSelected);
					seasonPreSelected = null;
				}
			}
			
			private function addSeason():void{
				seasonForm = new SeasonIndividualCRUDForm();
				seasonForm.loc = loc;
				seasonForm.isUpdate = false;
				seasonForm.addEventListener("saveSuccess",season_saveSuccess);
				seasonForm.addEventListener("cancelClicked",titleWindow_close);
				titleWindow = WindowsUtils.openDialog(loc.button.newLabel + ": " + loc.season.singular,seasonForm,this);
			}
			
			private function season_saveSuccess(evt:Object):void {
				seasons.addItem(seasonForm.currentSeason);
				WindowsUtils.autoSelectComboBox(seasonsCombo, seasonForm.currentSeason);
				PopUpManager.removePopUp(titleWindow);
			}
			
			private function titleWindow_close(evt:Object):void {
				PopUpManager.removePopUp(titleWindow);
			}
			
			//Room Type
			private function retrieveRoomTypes():void{
				var roomTypeServ:RoomTypeService = new RoomTypeService();
				roomTypeServ.retrieveAll(retrieveRoomTypes_resultHandler,faultHandler);
			}
			
			private function retrieveRoomTypes_resultHandler(event:ResultEvent,token:Object=null):void {
				//fills the ComboBox
				roomTypes.removeAll();
				roomTypes.addAll(event.result as ArrayCollection);
				//preselects a roomType
				if(roomTypePreSelected != null){
					WindowsUtils.autoSelectComboBox(roomTypesCombo,roomTypePreSelected);
					roomTypePreSelected = null;
				}
			}
			
			private function addRoomType():void{
				roomTypeForm = new RoomTypeIndividualCRUDForm();
				roomTypeForm.loc = loc;
				roomTypeForm.isUpdate = false;
				roomTypeForm.addEventListener("saveSuccess",roomType_saveSuccess);
				roomTypeForm.addEventListener("cancelClicked",titleWindow_close);
				titleWindow = WindowsUtils.openDialog(loc.button.newLabel + ": " + loc.roomType.singular,roomTypeForm,this);
			}
			
			private function roomType_saveSuccess(evt:Object):void {
				roomTypes.addItem(roomTypeForm.currentRoomType);
				WindowsUtils.autoSelectComboBox(roomTypesCombo, roomTypeForm.currentRoomType);
				PopUpManager.removePopUp(titleWindow);
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<mx:NumberValidator id="seasonValidator"
							enabled="false"
							minValue="0"
							lowerThanMinError="{loc.validator.requiredFieldError}" 
							source="{seasonsCombo}"
							property="selectedIndex"
							/>
		<mx:NumberValidator id="roomTypeValidator"
							enabled="false"
							minValue="0"
							lowerThanMinError="{loc.validator.requiredFieldError}" 
							source="{roomTypesCombo}"
							property="selectedIndex"
							/>
		<mx:NumberValidator id="seasonPriceValidator"
							required="true"
							requiredFieldError="{loc.validator.requiredFieldError}"
							minValue="0"
							domain="real"
							allowNegative="false"
							precision="3"
							precisionError="{loc.validator.precisionError}"
							separationError="{loc.validator.invalidFieldValueError}"
							negativeError="{loc.validator.invalidFieldValueError}"
							lowerThanMinError="{loc.validator.invalidFieldValueError}"
							invalidCharError="{loc.validator.invalidFieldValueError}"
							invalidFormatCharsError="{loc.validator.invalidFieldValueError}"
							decimalPointCountError="{loc.validator.invalidFieldValueError}"
							source="{priceText}"
							property="text"
							/>
		
	</fx:Declarations>
	<s:Rect width="100%" height="100%">
		<s:stroke>
			<s:SolidColorStroke color="{Colors.YELLOW_COLOR}" weight="1"/>
		</s:stroke>
		<s:fill><s:SolidColor color="{Colors.GRAY_COLOR}"/></s:fill>
	</s:Rect>
	<s:VGroup width="100%" height="100%">
		<s:HGroup width="100%" horizontalAlign="center">
			<s:Form>
				<s:layout>
					<s:FormLayout gap="-14"/>
				</s:layout>  
				
				<s:FormItem id="seasonItem" label="{loc.season.singular}:">
					<s:HGroup>
						<s:ComboBox id="seasonsCombo" dataProvider="{seasons}" labelField="name" requireSelection="true"/>
						<s:Button width="24" height="24" click="retrieveSeasons();"
								  icon="{Icons.refresh}" toolTip="{loc.button.refresh}"/>
						<s:Button width="24" height="24" click="addSeason();" icon="{Icons.add}"
								  toolTip="{loc.button.add}"/>
					</s:HGroup>
				</s:FormItem>
				
				<s:FormItem id="roomTypeItem" label="{loc.roomType.singular}:">
					<s:HGroup>
						<s:ComboBox id="roomTypesCombo" dataProvider="{roomTypes}" labelField="name" requireSelection="true"/>
						<s:Button width="24" height="24" click="retrieveRoomTypes();"
								  icon="{Icons.refresh}" toolTip="{loc.button.refresh}"/>
						<s:Button width="24" height="24" click="addRoomType();" icon="{Icons.add}"
								  toolTip="{loc.button.add}"/>
					</s:HGroup>
				</s:FormItem>
				
				<s:FormItem label="{loc.rate.priceperday}:" required="true">
					<s:TextInput id="priceText"/>
				</s:FormItem>
			</s:Form>
		</s:HGroup>
		<!-- BUTTONS -->
		<s:HGroup width="100%" height="100%" horizontalAlign="center" verticalAlign="middle">
			<s:Button label="{loc.button.save}" icon="{Icons.save}" click="save()"/>
			<s:Button label="{loc.button.cancel}" icon="{Icons.cancel}" click="cancel()"/>
		</s:HGroup>
		<s:Spacer height="5"/>
	</s:VGroup>
</s:Group>
