<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%" xmlns:individual="view.forms.crud.individual.*" xmlns:components="view.components.*"
		  creationComplete="init(event)">
	<fx:Script>
		<![CDATA[
			import model.Country;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import resources.Colors;
			
			import services.crud.CountryService;
			
			import spark.events.GridSelectionEvent;
			
			import utils.FaultHandler;
			
			[Bindable]
			public var loc:Object;
			
			[Bindable]
			private var countries:ArrayCollection = new ArrayCollection();
			
			private function retrieveAll_resultHandler(event:ResultEvent,token:Object=null):void {
				countries.removeAll();
				countries.refresh();
				countries.addAll(event.result as ArrayCollection);
			}
			
			
			private function delete_resultHandler(event:ResultEvent,token:Object=null):void {
				refreshGrid();
			}
			
			
			protected function init(event:FlexEvent):void{
				refreshGrid();
				createContextMenu();
			}
			
			protected function refreshGrid(event:Event=null):void
			{
				grid.selectedIndex = -1;
				individual.enabled = false;
				retrieveCountries();
			}
			
			private function retrieveCountries():void{
				new CountryService().retrieveAll(retrieveAll_resultHandler,FaultHandler.defaultFaultHandler);
			}
			
			protected function grid_selectionChangeHandler(event:GridSelectionEvent):void
			{
				if(grid.selectedIndex >= 0){
					individual.currentCountry = grid.selectedItem as Country;
				}
				individual.enabled = false;
			}
			
			protected function modifyCountry():void
			{
				individual.currentCountry = grid.selectedItem as Country;
				individual.isUpdate = true;
				individual.enabled=true;
				
			}
			
			protected function deleteCountry():void
			{
				new CountryService().deleteRecord(grid.selectedItem as Country,delete_resultHandler,FaultHandler.defaultFaultHandler);
			}
			
			protected function addCountry():void
			{
				grid.selectedIndex = -1;
				individual.currentCountry = new Country();
				individual.isUpdate = false;
				individual.enabled = true;
			}
			
			public function refreshForm():void{
				retrieveCountries();
			}
			
			// Creates a ContextMenu
			private function createContextMenu():void
			{
				var myContextMenu:ContextMenu = new ContextMenu();
				this.contextMenu = myContextMenu;       //add menu items
				var refreshItem:ContextMenuItem = new ContextMenuItem(loc.button.refresh);
				myContextMenu.customItems = [refreshItem];
				
				//adds event listener to menu item
				refreshItem.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, refreshGrid);
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<mx:VDividedBox height="100%" width="100%">
		<s:HGroup width="100%" height="70%">
			<s:DataGrid id="grid" width="100%" height="100%" dataProvider="{countries}" selectionChange="grid_selectionChangeHandler(event)" alternatingRowColors="{Colors.ALTERNATING_ROW_COLORS}" skinClass="view.skins.CustomDataGridSkin">
				<s:columns>
					<s:ArrayList>
						<s:GridColumn dataField="name" headerText="{loc.country.singular}"></s:GridColumn>
					</s:ArrayList>
				</s:columns>
			</s:DataGrid>
			<!-- BUTTONS -->
			<components:CRUDButtons height="100%" refreshClicked="refreshGrid()" addClicked="addCountry()"
									modifyClicked="modifyCountry()" removeClicked="deleteCountry()"
									loc="{loc}" modifyButtonEnabled="{grid.selectedIndex >= 0}"
									removeButtonEnabled="{grid.selectedIndex >= 0}"/>
		</s:HGroup>
		<individual:CountryIndividualCRUDForm id="individual" enabled="false" loc="{loc}" saveSuccess="refreshGrid();" cancelClicked="individual.enabled=false;" height="30%"/>
	</mx:VDividedBox>
</s:VGroup>

