<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%"
		 >
	<fx:Script>
		<![CDATA[
			import locales.Locale;
			
			import model.Alternative;
			import model.Occupation;
			import model.ReservationForm;
			import model.Room;
			import model.StateReservationForm;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.events.FlexEvent;
			
			import spark.components.gridClasses.GridColumn;
			import spark.skins.spark.DefaultGridItemRenderer;
			
			import utils.DateTimeUtils;
			
			[Bindable]
			private var calendarRows:ArrayCollection;
			
			private var roomIdToCalendarRowMap:Dictionary;

			private const IT_IS_AN_ALTERNATIVE:String = "Alternative";

			[Bindable]
			public var loc:Object = Locale.getInstance().getCurrentLocale();
			
			public var occupations:ArrayCollection = new ArrayCollection();
			
			public var dateFrom:Date;
			
			public var dateTo:Date;
			
			public var roomsToShow:ArrayCollection; //roomsToShow even if these don't have occupation
			
			
			private function addRoomColumn():void{
				var roomColumn:GridColumn = new GridColumn();
				roomColumn.headerText = loc.room.singular;
				roomColumn.dataField = "room.number";
				grid.columns.addItem(roomColumn);
			}
			
			private function addDayColumns(dateFrom:Date,dateTo:Date):void{
				var i:int = 0;
				while(DateTimeUtils.rollDaysToDate(dateFrom,i) <= dateTo){
					var auxDate:Date = DateTimeUtils.rollDaysToDate(dateFrom,i);
					var col:GridColumn = DateTimeUtils.getColumnForDate(auxDate,"day_" + i);
					col.labelFunction = calendarFormat;
					col.itemRendererFunction = selectRenderer;

					grid.columns.addItem(col);
					i++;
				}
			}
			
			/**
			 * Chooses a renderer dependendig on "column"
			 */ 
			private function selectRenderer(item:Object, column: GridColumn):IFactory
			{
				if(item!=null){
					return new ClassFactory(view.components.CalendarCustomCellRenderer);
				}else{
					return new ClassFactory(DefaultGridItemRenderer);
				}
			}

			private function calendarFormat(item:Object,column:GridColumn):String
			{
				var qtyAlternative:int = 0;
				var qtyConfirmed:int = 0;
				var qtyPreBooking:int = 0;
				var qtyCancelled:int = 0;
				var qtyExpired:int = 0;
				
				var result:String = '';
				var value:ArrayCollection = item [column.dataField] as ArrayCollection;
				if(value != null){
					for each (var o:Object in value){
					     if (o is String){
							 qtyAlternative++;
						 }else if (o is ReservationForm){
							 var resForm:ReservationForm = o as ReservationForm;
							 switch(resForm.state){
								 case StateReservationForm.CONFIRMED:
									 qtyConfirmed++;
									 break;
								 case StateReservationForm.PRE_BOOKING:
									 qtyPreBooking++;
									 break;
								 case StateReservationForm.CANCELLED:
									 qtyCancelled++;
									 break;
								 case StateReservationForm.EXPIRED:
									 qtyExpired++;
									 break;
							 }
						 }
					}
					//Calculate result based on quantities
					if(qtyConfirmed>0){
						result += StateReservationForm.convertToLabel(StateReservationForm.CONFIRMED,loc);
					}
					if(qtyPreBooking>0){
						if (result.length > 0 ) {result += ", "}
						result += StateReservationForm.convertToLabel(StateReservationForm.PRE_BOOKING,loc);
					}
					if(qtyCancelled>0){
						if (result.length > 0 ) {result += ", "}
						result += StateReservationForm.convertToLabel(StateReservationForm.CANCELLED,loc);
					}
					if(qtyExpired>0){
						if (result.length > 0 ) {result += ", "}
						result += StateReservationForm.convertToLabel(StateReservationForm.EXPIRED,loc);
					}
					if(qtyAlternative>0){
						if (result.length > 0 ) {result += ", "}
						result += "X";
					}

				}
				
				return result;
			
			}
			
			private function loadRoomsToShow():void{
				for each (var room:Room in roomsToShow){
					var roomId:Object = room.id;
					var row:Object = new Object();
					row.room = room;
					calendarRows.addItem(row);
					roomIdToCalendarRowMap[roomId] = row;
				}
			}
			
			public function init():void
			{
				roomIdToCalendarRowMap = new Dictionary();
				calendarRows = new ArrayCollection();

				loadRoomsToShow();
				//adds columns to the grid
				grid.columns = new ArrayList();
				addRoomColumn();
				addDayColumns(dateFrom,dateTo);
				
				for each (var occupation:Occupation in occupations){
					var isNew:Boolean = false;
					var roomId:Object = occupation.id.room.id;
					var row:Object = roomIdToCalendarRowMap[roomId];
					var attributeName:String = "day_" + DateTimeUtils.getQuantityOfDays(dateFrom,occupation.id.date);
					
					if( row[attributeName] == null){
						//every cell has an ArrayCollection, could have more than 1 reservationForm
						row[attributeName] = new ArrayCollection();
					}
					var cellArray:ArrayCollection = row[attributeName] as ArrayCollection;
					//alternatives In Calendar have reservationForm as Null
					if( occupation.id.reservationForm == null){ //it's an alternative
						cellArray.addItem(IT_IS_AN_ALTERNATIVE);
					}else{
						cellArray.addItem(occupation.id.reservationForm);
					}
					
				}
				
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:DataGrid id="grid" width="100%" height="100%" dataProvider="{calendarRows}" skinClass="view.skins.CustomDataGridSkin"/>
</s:Group>
