<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%" xmlns:individual="view.forms.crud.individual.*" xmlns:components="view.components.*"
		  creationComplete="init(event)">
	<fx:Script>
		<![CDATA[
			import model.Room;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import resources.Colors;
			
			import services.crud.RoomService;
			
			import spark.events.GridSelectionEvent;
			
			import utils.FaultHandler;
			
			[Bindable]
			public var loc:Object;
			
			[Bindable]
			private var rooms:ArrayCollection = new ArrayCollection();
			
			private function retrieveAll_resultHandler(event:ResultEvent,token:Object=null):void {
				grid.selectedItem = null;
				rooms.removeAll();
				rooms.addAll(event.result as ArrayCollection);
			}
			
			
			private function delete_resultHandler(event:ResultEvent,token:Object=null):void {
				refreshGrid();
			}
			
			
			protected function init(event:FlexEvent):void{
				refreshGrid();
			}
			
			protected function refreshGrid():void
			{
				individual.enabled = false;
				grid.selectedItem = null;
				new RoomService().retrieveAll(retrieveAll_resultHandler,FaultHandler.defaultFaultHandler);
			}
			
			protected function grid_selectionChangeHandler(event:GridSelectionEvent):void
			{
				if(grid.selectedItem !=null){
					individual.currentRoom = grid.selectedItem as Room;
				}
				individual.enabled = false;
			}
			
			protected function modifyRoom():void
			{
				individual.currentRoom = grid.selectedItem as Room;
				individual.isUpdate = true;
				individual.enabled=true;
				
			}
			
			protected function deleteRoom():void
			{
				new RoomService().deleteRecord(grid.selectedItem as Room,delete_resultHandler,FaultHandler.defaultFaultHandler);
			}
			
			protected function addRoom():void
			{
				grid.selectedItem = null;
				var room:Room = new Room();
				room.number = calculateNextRoomNumber();
				individual.currentRoom = room;
				individual.isUpdate = false;
				individual.enabled = true;
			}
			
			private function calculateNextRoomNumber():int{
				var numberToRoomMap:Dictionary = new Dictionary();
				var result:int = 0;
				var exists:Boolean = true;
				
				for each (var room:Room in rooms){
					numberToRoomMap[room.number] = room;
				}
				
				while(exists){
					result ++;
					if(numberToRoomMap[result]==null){
						exists = false;
					}
				}
				
				
				return result;
			}
			
			private function booleanFormat(item:Object,column:GridColumn):String
			{
				var value:Boolean = item [column.dataField] as Boolean; 
				if(value){
					return loc.boolean.trueValue;
				}else{
					return loc.boolean.falseValue;
				}
				
			}
			
			public function refreshForm():void{
				//TODO Complete this method
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<mx:VDividedBox height="100%" width="100%">
		<s:HGroup width="100%" height="50%">
			<s:DataGrid id="grid" width="100%" height="100%" dataProvider="{rooms}" selectionChange="grid_selectionChangeHandler(event)" alternatingRowColors="{Colors.ALTERNATING_ROW_COLORS}" skinClass="view.skins.CustomDataGridSkin">
				<s:columns>
					<s:ArrayList>
						<s:GridColumn dataField="number" headerText="{loc.room.number}"></s:GridColumn>
						<s:GridColumn dataField="roomType.name" headerText="{loc.roomType.singular}"></s:GridColumn>
						<s:GridColumn dataField="peopleQuantity" headerText="{loc.room.peopleQuantity}"></s:GridColumn>
						<s:GridColumn dataField="withMaritalBed" headerText="{loc.room.withMaritalBed}" labelFunction="booleanFormat"></s:GridColumn>
						<s:GridColumn dataField="withExtraBed" headerText="{loc.room.withExtraBed}" labelFunction="booleanFormat"></s:GridColumn>
						<s:GridColumn dataField="description" headerText="{loc.room.description}"></s:GridColumn>
					</s:ArrayList>
				</s:columns>
			</s:DataGrid>
			<!-- BUTTONS -->
			<components:CRUDButtons height="100%" refreshClicked="refreshGrid()" addClicked="addRoom()"
									modifyClicked="modifyRoom()" removeClicked="deleteRoom()"
									loc="{loc}" modifyButtonEnabled="{grid.selectedItem!=null}"
									removeButtonEnabled="{grid.selectedItem!=null}"/>
		</s:HGroup>
		<individual:RoomIndividualCRUDForm id="individual" enabled="false" loc="{loc}" saveSuccess="refreshGrid();" cancelClicked="individual.enabled=false;" height="50%"/>
	</mx:VDividedBox>
</s:VGroup>

